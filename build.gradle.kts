plugins {
    id("org.openrewrite.build.recipe-library") version "latest.release"
}

group = "org.openrewrite.recipe"
description = "Netty Migration"

recipeDependencies {
    parserClasspath("io.netty:netty-transport:4.2+")
    parserClasspath("io.netty:netty-transport-classes-epoll:4.2+")
    parserClasspath("io.netty:netty-codec-base:4.2+")
    parserClasspath("io.netty:netty-common:4.2+")
    parserClasspath("org.jboss.netty:netty:3.2.+")
}

val rewriteVersion = rewriteRecipe.rewriteVersion.get()
dependencies {
    implementation(platform("org.openrewrite:rewrite-bom:$rewriteVersion"))
    implementation("org.openrewrite:rewrite-java")
    implementation("org.openrewrite.recipe:rewrite-java-dependencies:$rewriteVersion")
    implementation("org.openrewrite:rewrite-templating:$rewriteVersion")

    annotationProcessor("org.openrewrite:rewrite-templating:$rewriteVersion")
    compileOnly("com.google.errorprone:error_prone_core:2.+") {
        exclude("com.google.auto.service", "auto-service-annotations")
        exclude("io.github.eisop","dataflow-errorprone")
    }
    compileOnly("io.netty:netty-all:4.2.+")

    testImplementation("org.openrewrite:rewrite-java-21")
    testImplementation("org.openrewrite:rewrite-gradle")
    testImplementation("org.openrewrite.gradle.tooling:model:$rewriteVersion")
    testImplementation("org.openrewrite:rewrite-maven")
    testImplementation("org.openrewrite:rewrite-properties")
    testImplementation("org.openrewrite:rewrite-test")

    testRuntimeOnly(gradleApi())
    testRuntimeOnly("org.jboss.netty:netty:3.2.+")
    testRuntimeOnly("io.netty.incubator:netty-incubator-transport-classes-io_uring:0.0.26.Final")
    testRuntimeOnly("org.openrewrite.recipe:rewrite-spring:6.25.1")
}

tasks.withType<JavaCompile> {
    options.compilerArgs.add("-Arewrite.javaParserClasspathFrom=resources")
}

configurations.all {
    resolutionStrategy.componentSelection.all {
        if (candidate.group == "io.netty" && candidate.version.endsWith("-SNAPSHOT")) {
            reject("Reject Netty SNAPSHOTs so dynamic 4.2.+ selectors only match released versions")
        }
    }
}
